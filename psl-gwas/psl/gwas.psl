// Analogous to the non-pop-structure-adjusted correlation of the kmer to the pheno
1.0: Block(K, P) & Contains(S, K) & SamplePheno(S, P) >> KmerPheno(K, P) ^2
0.5: Block(K, P) & Contains(S, K) & !SamplePheno(S, P) >> !KmerPheno(K, P) ^2

// A kmer can cause two very similar phenos but probably does not cause two very dissimilar phenos
0.5: Block(K, P1) & Block(K, P2) & SimilarPheno(P1, P2) & KmerPheno(K, P1) & (P1 != P2) >> KmerPheno(K, P2) ^2
0.3: Block(K, P1) & Block(K, P2) & !SimilarPheno(P1, P2) & KmerPheno(K, P1) & (P1 != P2) >> !KmerPheno(K, P2) ^2

// If two samples are really similar and one displays a pheno and the other doesn’t, the kmers they don’t share are responsible.
// This is more tailored toward finding SNPs/indels.
1.0: Block(K, P) & SimilarSample(S1, S2) & SamplePheno(S1, P) & !SamplePheno(S2, P) & Contains(S1, K) & !Contains(S2, K) & (S1 != S2) >> KmerPheno(K, P) ^2

// If two samples are really dissimilar and both display a pheno, the kmers they share are responsible.
// This is more tailored toward finding genes.
0.6: Block(K, P) & DissimilarSample(S1, S2) & SamplePheno(S1, P) & SamplePheno(S2, P) & Contains(S1, K) & Contains(S2, K) & (S1 != S2) >> KmerPheno(K, P) ^2
0.2: Block(K, P) & !DissimilarSample(S1, S2) & SamplePheno(S1, P) & SamplePheno(S2, P) & Contains(S1, K) & Contains(S2, K) & (S1 != S2) >> !KmerPheno(K, P) ^2

// Negative prior: most kmers do not cause a pheno.
0.5: !KmerPheno(K, P) ^2

